# mise - ランタイムバージョン管理（node, python, go等）
eval "$(mise activate zsh)"

alias sail="bash vendor/bin/sail"

# Git公式のgit-prompt.shを使用してブランチ名を表示
source /opt/homebrew/etc/bash_completion.d/git-prompt.sh

# 変更状態を表示する設定
export GIT_PS1_SHOWDIRTYSTATE=1        # *=変更あり, +=ステージング済み
export GIT_PS1_SHOWUNTRACKEDFILES=1    # %=未追跡ファイルあり
export GIT_PS1_SHOWSTASHSTATE=1        # $=stashあり

setopt PROMPT_SUBST
PROMPT='%n@%m %F{blue}%~%f %F{208}$(__git_ps1 "(%s)")%f%# '

# コマンド補完候補を薄い文字で表示
source /opt/homebrew/share/zsh-autosuggestions/zsh-autosuggestions.zsh

# Claude Code - 常にMCP設定を読み込む
alias claude='command claude --mcp-config ~/.claude/mcp.json'

# トンネリング設定（環境固有）
[ -f ~/.zsh_tunneling ] && source ~/.zsh_tunneling

# モダンCLIツールのエイリアス
alias cat="bat"
alias ls="eza --icons --git"
alias ll="eza -la --icons --git"
alias la="eza -a --icons --git"
alias tree="eza --tree --icons"
alias grep="rg"

# fzf 初期化・キーバインド（Ctrl+R: 履歴検索, Ctrl+T: ファイル検索）
source <(fzf --zsh)

# fzf デフォルトオプション（fdを使用、隠しファイル含む、.gitは除外）
export FZF_DEFAULT_COMMAND='fd --type f --hidden --follow --exclude .git'
export FZF_DEFAULT_OPTS='--height 40% --layout=reverse --border --preview "bat --style=numbers --color=always --line-range :300 {}" --preview-window=right:50%'
export FZF_CTRL_T_COMMAND="$FZF_DEFAULT_COMMAND"
export FZF_ALT_C_COMMAND='fd --type d --hidden --follow --exclude .git'

# ghq + fzf: リポジトリ選択してcd（Ctrl+Gで起動）
ghq-fzf() {
  local repo
  repo=$(ghq list -p | fzf --preview "eza --icons --git -la {}")
  if [[ -n "$repo" ]]; then
    cd "$repo"
    zle accept-line
  else
    zle reset-prompt
  fi
}
zle -N ghq-fzf
bindkey '^G' ghq-fzf

# ghqで取得してすぐcdするショートカット
ghq-get-cd() {
  ghq get "$@" && cd "$(ghq list -p | fzf --query "${@##*/}" --select-1)"
}
alias gg='ghq-get-cd'

# nvim + tmux: 右にClaude用ターミナルを分割して起動
nvc() {
  local target="${1:-.}"
  if [[ -n "$TMUX" ]]; then
    local split_pane_id
    split_pane_id=$(tmux split-window -h -c "$(pwd)" -p 40 -P -F '#{pane_id}')
    tmux select-pane -L
    # nvimペインだけallow-passthrough有効化（image.nvim用）
    tmux set-option -p allow-passthrough on
    command nvim "$target"
    tmux kill-pane -t "$split_pane_id" 2>/dev/null
    return
  fi
  local session_name="nvc-$$-$RANDOM"
  tmux new-session -d -s "$session_name" -c "$(pwd)"
  tmux send-keys -t "$session_name" "nvim $target; tmux kill-session -t $session_name" Enter
  tmux split-window -h -t "$session_name" -c "$(pwd)" -p 10
  tmux select-pane -t "$session_name:1.1"
  tmux attach-session -t "$session_name"
}
alias vim='nvc'

# ヘルプ表示（glowがあれば使う、なければless）
_show_md() {
  if command -v glow &> /dev/null; then
    glow -p "$1"
  else
    less "$1"
  fi
}
alias dothelp='_show_md ~/.local/share/chezmoi/README.md'
alias vimhelp='_show_md ~/.config/nvim/doc/README.md'

# Chrome - 新しいウィンドウで開く
alias chrome='open -na "Google Chrome" --args --new-window'

# タイポ修正（遅延ロード: 初回呼び出し時にのみ読み込み）
fuck() {
  unfunction fuck
  eval $(thefuck --alias)
  fuck "$@"
}
